apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java' // Needed for shadow to work
apply plugin: 'maven-publish'
assemble.dependsOn shadowJar

dependencies {
    implementation "org.apache.poi:poi-ooxml:$poiVersion"
    implementation 'org.apache.xmlbeans:xmlbeans:3.1.0'
    implementation 'com.fasterxml:aalto-xml:1.2.2'

    if (project.hasProperty('poiType') && poiType == "extended") {
        switch (poiVersion) {

            // 3.5 .. 3.6
            case ~/^(3\.5.*|3\.6.*)/:
                ext.ooxml_extendedVersion = "1.0"
                break;

            // 3.7 - 3.13
            case ~/^(3\.7.*|3\.([7-9]|1[0-3]).*)/:
                ext.ooxml_extendedVersion = "1.1"
                break

            // 3.13 - 3.17
            case ~/^(3\.1[4-7].*)/:
                ext.ooxml_extendedVersion = "1.3"
                break

            // 4.0.0 +
            default:
                ext.ooxml_extendedVersion = "1.4"
                break
        }
        implementation "org.apache.poi:ooxml-schemas:$ooxml_extendedVersion"
    }
}

shadowJar {
    archiveName 'poi-all.jar'

    exclude 'schemaorg_apache_xmlbeans/attribute/**'
    exclude 'schemaorg_apache_xmlbeans/attributegroup/**'
    exclude 'schemaorg_apache_xmlbeans/element/**'
    exclude 'schemaorg_apache_xmlbeans/identityconstraint/**'
    exclude 'schemaorg_apache_xmlbeans/javaname/**'
    exclude 'schemaorg_apache_xmlbeans/modelgroup/**'
    exclude 'schemaorg_apache_xmlbeans/namespace/**'
    exclude 'schemaorg_apache_xmlbeans/src/**'
    exclude 'schemaorg_apache_xmlbeans/system/s8*/**'
    exclude 'schemaorg_apache_xmlbeans/system/sXML*/**'
    exclude 'schemaorg_apache_xmlbeans/type/**'

    exclude 'repackage/**'

    exclude 'LICENSE.txt'
    exclude 'NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/maven/**'

    // these are only needed when handling Visio files, remove it if you would like to use Visio support
//    dependencies {
//        exclude(dependency('com.github.virtuald:curvesapi'))
//        exclude(dependency('commons-codec:commons-codec'))
//    }

    // Relocate javax dependencies so Android does not choke
    relocate 'javax.xml.namespace', 'org.apache.poi.javax.xml.namespace'
    relocate 'javax.xml.stream', 'org.apache.poi.javax.xml.stream'
    relocate 'javax.xml.XMLConstants', 'org.apache.poi.javax.xml.XMLConstants'
}

artifacts {
    compile shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.supercilex.poiandroid'
            artifactId 'poi'
            version poiVersion

            artifact file("$buildDir/libs/poi-all.jar")

            pom.withXml {
                asNode().with {
                    appendNode('packaging', 'jar')
                    appendNode('name', "POI Android")
                    appendNode('description', 'Apache POI for Android')
                }
            }
        }
    }
}
